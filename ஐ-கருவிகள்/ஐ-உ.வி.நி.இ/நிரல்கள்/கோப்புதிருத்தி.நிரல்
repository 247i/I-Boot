; -------- Configuration and Text File Manipulation Stuff! --------

Function கோப்பில்எழுது ; modified to populate *.cfg file with the distro user installed!
 Exch $R0 ;file to write to
 Exch
 Exch $1 ;text to write

;wrapper before write. Assumption UTF-8 file. 
${FileRecode} "$BDir\அகர\பகவன்\$DistroPath" "ToUTF16LE"
;Convert file from UTF-8 to UTF16LE
 FileOpen $R0 '$BDir\அகர\பகவன்\$DistroPath' a 
 FileSeek $R0 0 END
;FileWrite $R0 '$\r$\n$1' ; புதிய வரி மற்றும் தரவைச் சேர்க்கவும்
 FileWriteUTF16LE $R0 '$\r$\n$1' ; புதிய வரி மற்றும் தரவைச் சேர்க்கவும்
 FileClose $R0
; Convert file back to UTF-8
${FileRecode} "$BDir\அகர\பகவன்\$DistroPath" "ToUTF8"
; Convert file UTF16LE to UTF-8
 
 Pop $1
 Pop $R0
FunctionEnd

!macro கோப்பில்எழுது String File
 Push "${String}"
 Push "${File}"
 Call கோப்பில்எழுது
 ${LineFind} "$BDir\அகர\பகவன்\$DistroPath" "$BDir\அகர\பகவன்\$DistroPath" "1:-1" "வெற்றுவரியைநீக்கு" ; Remove any left over empty lines
!macroend  
!define கோப்பில்எழுது "!insertmacro கோப்பில்எழுது"


Function கணினிகோப்பில்எழுது ; Write entry to கணிலினக்சு.உலகு
 Exch $R0 ;file to write to
 Exch
 Exch $1 ;text to write
;wrapper before write. Assumption UTF-8 file. 
${FileRecode} '$BDir\அகர\பகவன்\முதற்றே.உலகு' "ToUTF16LE"
;Convert file from UTF-8 to UTF16LE
 FileOpen $R0 '$BDir\அகர\பகவன்\முதற்றே.உலகு' a 
 FileSeek $R0 0 END
;FileWrite $R0 '$\r$\n$1$\r$\n'
 FileWriteUTF16LE $R0 '$\r$\n$1$\r$\n'
 FileClose $R0
; Convert file back to UTF-8
${FileRecode} '$BDir\அகர\பகவன்\முதற்றே.உலகு' "ToUTF8"
; Convert file UTF16LE to UTF-8
 Pop $1
 Pop $R0
FunctionEnd
!macro கணினிகோப்பில்எழுது String File
  Push "${String}"
  Push "${File}"
  Call கணினிகோப்பில்எழுது
!macroend  
!define கணினிகோப்பில்எழுது "!insertmacro கணினிகோப்பில்எழுது"

Function நிறுவப்பட்டபட்டியல் ; Creates a list of installed distros in the அகர\பகவன் folder on the USB drive (So we can uninstall the distros later)
 ${IfNot} ${FileExists} "$BDir\!\$JustISOName\*.*" ; If the installation directory exists user must be reinstalling the same distro, so we won't add a removal entry. 
   Exch $R0 ;file to write to
   Exch
   Exch $1 ;text to write
   ${If} ${FileExists} "$BDir\அகர\பகவன்\நிறுவப்பட்டது.உரை" 
    FileOpen $R0 '$BDir\அகர\பகவன்\நிறுவப்பட்டது.உரை' a 
    FileSeek $R0 0 END
	FileWriteUTF16LE $R0 '$\r$\n$1' ; add subsequent entry on a new line
   ${Else}
    FileOpen $R0 '$BDir\அகர\பகவன்\நிறுவப்பட்டது.உரை' a 
    FileSeek $R0 0 END
	FileWriteUTF16LE /BOM $R0 '$1'  ; add first entry without a new line
   ${EndIf}
    FileClose $R0

    Pop $1
    Pop $R0
 ${EndIf}
FunctionEnd
!macro நிறுவப்பட்டபட்டியல் String File
  Push "${String}"
  Push "${File}"
  Call நிறுவப்பட்டபட்டியல்
!macroend  
!define நிறுவப்பட்டபட்டியல் "!insertmacro நிறுவப்பட்டபட்டியல்"

Function ஒழுங்கமை ; ஐ உடன் பயன்படுத்த திருத்தப்பட்ட முன்னணி மற்றும் பின்னால் உள்ள இடைவெளியை அகற்று
	Exch $R1 ; Original string
	Push $R2
Loop:
	StrCpy $R2 "$R1" 1
	StrCmp "$R2" " " TrimLeft
	StrCmp "$R2" "$\r" TrimLeft
	StrCmp "$R2" "$\n" TrimLeft
	StrCmp "$R2" "$\t" TrimLeft
	GoTo Loop2
TrimLeft:	
	StrCpy $R1 "$R1" "" 1
	Goto Loop
Loop2:
	StrCpy $R2 "$R1" 1 -1
	StrCmp "$R2" " " TrimRight
	StrCmp "$R2" "$\r" TrimRight
	StrCmp "$R2" "$\n" TrimRight
	StrCmp "$R2" "$\t" TrimRight
	GoTo Done
TrimRight:	
	StrCpy $R1 "$R1" -1
	Goto Loop2
Done:
	Pop $R2
	Exch $R1
FunctionEnd
!macro ஒழுங்கமை TrimmedString OriginalString
  Push "${OriginalString}"
  Call ஒழுங்கமை
  Pop "${TrimmedString}"
!macroend
!define ஒழுங்கமை "!insertmacro ஒழுங்கமை" 

Function அகற்றும்பட்டியல் ; Lists the distros installed on the select drive.
 ${NSD_SetText} $LinuxDistroSelection "படி 2: $DestDiskஇலிருந்து அகற்ற தேர்வு"  
 ${If} ${FileExists} "$BDir\அகர\பகவன்\நிறுவப்பட்டது.உரை" ; Are there distributions on the select drive? 
 ClearErrors
 FileOpen $0 $BDir\அகர\பகவன்\நிறுவப்பட்டது.உரை r
  loop:
   FileReadUTF16LE $0 $1
    IfErrors done
    StrCpy $DistroName $1
	${ஒழுங்கமை} "$DistroName" "$DistroName" ; Remove spaces, newlines, and carriage return
    ${NSD_CB_AddString} $Distro "$DistroName" ; Add DistroName to the listbox of removable distros ; was ${NSD_LB_AddString} $Distro "$DistroName" ; Enable for DropBox
   Goto loop
  done:  
 FileClose $0

 ${Else}
     ;Call உதநிகோப்புபெயர்அமை
 ${EndIf}
FunctionEnd

!include "TextFunc.nsh" ; TextFunc.nsh required for the following நிறுவலைநீக்கு function
Function நிறுவலைநீக்கு  ; நிறுவப்பட்டது.உரைஇலிருந்து உள்ளீட்டைத் தேர்ந்தெடு நீக்குகிறது          
	StrLen $0 "$DistroName"
	StrCpy $1 "$R9" $0
	StrCmp $1 "$DistroName" 0 End
	StrCpy $R9 ""
	End:
	Push $0
FunctionEnd

Function வெற்றுவரியைநீக்கு ;நிறுவப்பட்டது.உரையிலிருந்து வெற்று வரியை நீக்குகிறது
	StrLen $0 "$\r$\n"
	StrCpy $1 "$R9" $0
	StrCmp $1 "$\r$\n" 0 End
	StrCpy $R9 ""
	End:
	Push $0
FunctionEnd

!define சரம்மாற்று "!insertmacro சரம்மாற்று"
 
!macro சரம்மாற்று ResultVar String SubString RepString
  Push "${String}"
  Push "${SubString}"
  Push "${RepString}"
  Call சரம்மாற்று
  Pop "${ResultVar}"
!macroend
 
Function சரம்மாற்று
/*After this point:
  ------------------------------------------
  $R0 = RepString (input)
  $R1 = SubString (input)
  $R2 = String (input)
  $R3 = RepStrLen (temp)
  $R4 = SubStrLen (temp)
  $R5 = StrLen (temp)
  $R6 = StartCharPos (temp)
  $R7 = TempStrL (temp)
  $R8 = TempStrR (temp)*/
 
  ;Get input from user
  Exch $R0
  Exch
  Exch $R1
  Exch
  Exch 2
  Exch $R2
  Push $R3
  Push $R4
  Push $R5
  Push $R6
  Push $R7
  Push $R8
 
  ;Return "String" if "SubString" is ""
  ${IfThen} $R1 == "" ${|} Goto Done ${|}
 
  ;Get "RepString", "String" and "SubString" length
  StrLen $R3 $R0
  StrLen $R4 $R1
  StrLen $R5 $R2
  ;Start "StartCharPos" counter
  StrCpy $R6 0
 
  ;Loop until "SubString" is found or "String" reaches its end
  ${Do}
    ;Remove everything before and after the searched part ("TempStrL")
    StrCpy $R7 $R2 $R4 $R6
 
    ;Compare "TempStrL" with "SubString"
    ${If} $R7 == $R1
      ;Split "String" to replace the string wanted
      StrCpy $R7 $R2 $R6 ;TempStrL
 
      ;Calc: "StartCharPos" + "SubStrLen" = EndCharPos
      IntOp $R8 $R6 + $R4
 
      StrCpy $R8 $R2 "" $R8 ;TempStrR
 
      ;Insert the new string between the two separated parts of "String"
      StrCpy $R2 $R7$R0$R8
      ;Now calculate the new "StrLen" and "StartCharPos"
      StrLen $R5 $R2
      IntOp $R6 $R6 + $R3
      ${Continue}
    ${EndIf}
 
    ;If not "SubString", this could be "String" end
    ${IfThen} $R6 >= $R5 ${|} ${ExitDo} ${|}
    ;If not, continue the loop
    IntOp $R6 $R6 + 1
  ${Loop}
 
  Done:
 
  ;Return output to user
  StrCpy $R0 $R2
 
/*After this point:
  ------------------------------------------
  $R0 = ResultVar (output)*/
 
  Pop $R8
  Pop $R7
  Pop $R6
  Pop $R5
  Pop $R4
  Pop $R3
  Pop $R2
  Pop $R1
  Exch $R0
FunctionEnd
